buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: "${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

group 'com.earlybirdapi'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1')

    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.0.2.RELEASE')
    compile(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.7')
    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.0'


    compile(group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.25.0.RELEASE')
    compile(group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2')
    compile(group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2')

    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.0.5.RELEASE')
    compile(group: 'com.google.cloud.sql', name: 'mysql-socket-factory-connector-j-8', version: '1.0.11')
    compile(group: 'com.h2database', name: 'h2', version: '1.4.197')
    
    compile group: 'org.springframework.security', name: 'spring-security-core', version: '5.1.2.RELEASE'

    compile(group: 'org.projectlombok', name: 'lombok', version: '1.18.2') {
        exclude(group: 'org.slf4j', module: 'jul-to-slf4j')
    }
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8'

    testCompile(group: 'junit', name: 'junit', version: '4.12')
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBootVersion}")

}

jacoco {
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['com/lhd/earlybirdapi/aspect/**',
                              'com/lhd/earlybirdapi/config/**',
                              'com/lhd/earlybirdapi/BroadcastApplication.class'])
        })
    }
}
test.finalizedBy jacocoTestReport

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
        }
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['com/lhd/earlybirdapi/aspect/**',
                              'com/lhd/earlybirdapi/config/**',
                              'com/lhd/earlybirdapi/BroadcastApplication.class'])
        })
    }
}
check.finalizedBy jacocoTestCoverageVerification

checkstyle {
    toolVersion = '8.4'
    configFile = new File(rootDir, "checkstyle.xml")
}
check.dependsOn checkstyleMain
check.dependsOn checkstyleTest
build.dependsOn check